plugins {
    id "java"
    id "maven-publish"
    id "name.remal.lombok" version "2.2.6"
}

group = 'org.dobrien.ea'
version = '0.0.0.rc-01'

repositories {
    mavenCentral()
    mavenLocal()
}

def log4jVersion = '2.23.1'
def jsoupVersion = '1.17.2'
def lombokVersion = '1.18.32'
def opencsvVersion = '5.8'
def poiVersion = '5.2.5'

dependencies {

    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

    implementation "org.jsoup:jsoup:${jsoupVersion}"
    implementation "com.opencsv:opencsv:${opencsvVersion}"
    implementation ("org.apache.poi:poi:${poiVersion}") {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }

    implementation ("org.apache.poi:poi-ooxml:${poiVersion}") {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }


    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

// Conflict resolution.

configurations.configureEach {
    // Fail eagerly on version conflict (includes transitive dependencies)
    // e.g. multiple different versions of the same dependency (group and name are equal)
    resolutionStrategy.failOnVersionConflict()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.implementation
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId =  project.group
            artifactId = project.name
            version = project.version
            from components.java
        }
    }
}